/*
 * Copyright 2000-2020 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.rodm:gradle-teamcity-plugin:1.0"
    }
}

apply plugin: 'java'
apply plugin: 'com.github.rodm.teamcity-server'

group 'org.jetbrains.teamcity'
version = "$System.env.BUILD_NUMBER"
if (version.length() == 0) {
    version = "SNAPSHOT"
}

dependencies {
    compile('com.google.code.gson:gson:2.3.1')
    compile('org.apache.httpcomponents:httpclient:4.5.2')
    testCompile('org.springframework:spring-test:4.3.2.RELEASE')
    testCompile('org.mockito:mockito-core:2.1.0-RC.1')
    provided(group: 'org.jetbrains.teamcity', name: 'oauth', version: "${TeamCityVersion}")
}

test {
    useTestNG()
}

ext {
    teamcityDir = "$rootDir/servers/TeamCity-${TeamCityVersion}"
    teamcityDataDir = "$rootDir/data/" + (
            (TeamCityVersion ==~ /(\d+\.\d+).*/) ?
                    ((TeamCityVersion =~ /(\d+\.\d+).*/)[0][1])
                    : TeamCityVersion
    )
    teamcityJavaHome = System.properties['java.home']
}

teamcity {
    version = TeamCityVersion

    server {
        descriptor {
            name = project.name
            displayName = 'TeamCity GitHub Auth'
            version = project.version
            vendorName = 'JetBrains, s.r.o.'
            vendorUrl = 'https://www.jetbrains.com/'
            description = 'Allows authenticating using GitHub OAuth'
            useSeparateClassloader = true
        }

        environments {
            teamcityDev {
                version = TeamCityVersion
                homeDir = file(teamcityDir)
                dataDir = file(teamcityDataDir)
                javaHome = file(teamcityJavaHome)
                serverOptions '-Xdebug'
                serverOptions '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5500'

                serverOptions '-Dteamcity.development.mode=true'
                serverOptions '-Dteamcity.development.shadowCopyClasses=true'
            }

        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}
